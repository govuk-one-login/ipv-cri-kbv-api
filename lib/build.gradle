plugins {
	id "java"
	id "jacoco"
	id "com.github.bjornvester.wsdl2java" version "1.2"
	id "io.freefair.aspectj.post-compile-weaving" version "6.3.0"
}

repositories {
	maven {
		url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
	}
}

dependencies {
	implementation configurations.otel

	implementation configurations.cri_common_lib,
			configurations.aws,
			configurations.dynamodb,
			configurations.cache,
			configurations.lambda,
			configurations.jackson,
			configurations.hibernate,
			configurations.nimbus,
			configurations.soap,
			configurations.sqs

	aspect configurations.powertools

	runtimeOnly configurations.logging_runtime
	testImplementation configurations.tests
	testRuntimeOnly configurations.test_runtime
}

java {
	withSourcesJar()
}

test {
	useJUnitPlatform ()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required.set(true)
	}
}

tasks.named("jar") {
	manifest {
		attributes(
				"Implementation-Title": project.name,
				"Implementation-Version": project.version
				)
	}
}

sourceSets {
	main {
		java {
			srcDirs = [
				'src/build/generated/sources/wsdl2java',
				'src/main/java'
			]
		}
	}
}

wsdl2java {
	includes = [
		'wsdl/iiq-wasp-token.wsdl',
		'wsdl/iiq-service.wsdl'
	]
}

tasks.sourcesJar.dependsOn 'wsdl2java'
